plugins {
  alias(libs.plugins.jmailen.kotlinter) apply(false)
  alias(libs.plugins.kotlin.jvm) apply(false)
}

subprojects {
  plugins.withId(libs.plugins.kotlin.jvm.get().pluginId) {
    pluginManager.apply(libs.plugins.jmailen.kotlinter.get().pluginId)
  }

  plugins.withId("java") {
    tasks.withType(JavaCompile).configureEach {
      options.release.set(Integer.valueOf(JavaVersion.VERSION_1_8.majorVersion))
    }
    java {
      toolchain {
        languageVersion.set(JavaLanguageVersion.of(JavaVersion.VERSION_17.majorVersion))
      }
    }
  }
  tasks.withType(Test).configureEach {
    useJUnitPlatform()
  }

  configurations.configureEach {
    resolutionStrategy.eachDependency {
      if (requested.group == "com.android.tools.build" && requested.name == "aapt2-proto") {
        useVersion(libs.versions.aapt2Proto.get())
        because("we need to keep depenedencies in sync with bundletool")
      }
      if (requested.group == "com.google.protobuf" && requested.name == "protobuf-java") {
        useVersion(libs.versions.protobufJava.get())
        because("we need to keep depenedencies in sync with bundletool")
      }
      if (requested.group == "com.google.guava" && requested.name == "guava") {
        useVersion(libs.versions.guava.get())
        because("we need to keep depenedencies in sync with bundletool")
      }
    }
  }

  plugins.withId('org.jetbrains.kotlin.jvm') {
    compileKotlin {
      kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = [
            "-progressive",
            '-opt-in=kotlin.contracts.ExperimentalContracts'
        ]
      }
    }
  }
}
